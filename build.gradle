import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java-library'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'org.black_ixx'
version = '3.2.7'

java {
    disableAutoTargetJvm()
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://nexus.hc.to/content/repositories/pub_releases' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.dakanilabs.com/repository/public' }
    maven { url = 'https://repo.maven.apache.org/maven2' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    api fileTree(dir: 'libs', includes: ['RoseGarden-1.3.0.35-DEV-SNAPSHOT.jar'])

    compileOnly 'org.spigotmc:spigot-api:1.20.5-R0.1-SNAPSHOT'
    compileOnly 'com.github.milkbowl:vault:1.7.3'
    compileOnly 'me.lokka30:treasury-api:1.2.1'
    compileOnly 'com.vexsoftware:votifier:2.7.2'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'me.realized:tokenmanager:3.2.5'
    compileOnly 'su.nightexpress:gamepoints:1.3.1'
    compileOnly 'su.nexmedia:nexengine:2.2.0'
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set(null)
    minimize()

    exclude ("module-info.class", "LICENSE", "LICENSE.txt", "META-INF/MANIFEST.MF", "META-INF/LICENSE", "META-INF/NOTICE", "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA", "META-INF/*.txt", "README", "INFO_BIN", "INFO_SRC")
    relocate('dev.rosewood.rosegarden', 'org.black_ixx.playerpoints.libs.rosegarden')
}

build.dependsOn shadowJar
